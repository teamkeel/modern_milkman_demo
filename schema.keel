// PRODUCTS
model Product {
    fields {
        title Text
        description Text
        imageUrl Text
        gtin Text?
        price Number
        size Text
        unitPrice Text?
        specialOfferDiscount Number
        specialOffer Boolean
        bundles ProductBundle[]
 }

    actions {
        get getProduct(id)
        create createProduct() with (title, description, imageUrl, price, size, specialOfferDiscount, specialOffer)
        list listProducts()
        update updateProduct(id) with (title?, description?, imageUrl?, gtin?)
        delete deleteProduct(id)
    }

    @permission(
        expression: true,
        actions: [list, create, update, delete, get]
    )
}

model Bundle {
    fields {
        title Text
        description Text
        imageUrl Text
        gtin Text?
        price Number
        size Text
        unitPrice Text?
        specialOfferDiscount Number
        specialOffer Boolean
        products ProductBundle[]
    }
}


model ProductBundle {
    fields {
        products Product
        bundles Bundle
    }
}



// CUSTOMERS
model Customer {
    fields {
        firstName Text
        lastName Text
        email Text
        phone Text
        marketingConsent Boolean
        deliveryAddress Text
    }

    actions {
        list listCustomers()
        get getCustomer(id)
        create createCustomer() with (firstName, lastName, email, phone, marketingConsent, deliveryAddress)
    }

    @permission(
        expression: true,
        actions: [list, create]
    )

    @on([create], slackmessage)
}

model Order {
    fields {
        customer Customer
        site Site
        status OrderStatus @default(OrderStatus.New)
        items OrderItems[]
        // totalPrice @computed(sum(items.price))
    }
    
    actions {
        create createOrder() with (customer.id, site.id, items.product.id, items.quantity, items.price, status)
        update updateOrder(id) with (customer.id, site.id, items.product.id, items.quantity, items.price, status)
    }

    @permission(
        expression: true,
        actions: [list, create]
    )   
}

enum OrderStatus {
    New
    Readytocollect
    Collected
    Cancelled
}

model OrderItems {
    fields {
        order Order
        product Product
        quantity Number
        price Number
    }
}


// OPERATIONS

model Site {
    fields {
        name Text
        address Text
        storageLocations StorageLocation[]
        currency Text?
    }

    actions {
        create createSite() with (name, address)
    }

    @permission(
        expression: true,
        actions: [create]
    )
}

model SiteProduct {
    fields {
        product Product
        locations SiteProductLocation[]
        price Number
    }

    actions {
        create createSiteProduct() with (product.id, locations.location.id, price)
        list listSiteProduct()
        update updateSiteProduct(id) with (price)
    }

    @permission(
        expression: true,
        actions: [list, create, update]
    )
}

model SiteProductLocation {
    fields {
        location StorageLocation
        quantity Number
        siteProduct SiteProduct
    }

    actions {
        create createStockItem() with (location.id, location.site.id, quantity, siteProduct.product.id, siteProduct.price)
        list listSiteProductLocation()
        get getStockItem(id)
        update updateSiteProductLocation(id) with (quantity)
    }

    @permission(
        expression: true,
        actions: [list, update, create, get]
    )
}

model StorageLocation {
    fields {
        name Text
        site Site
    }

    actions {
        create createStorageLocation() with (name, site.id)
        list listStorageLocations()
        get getStorageLocation(id)
    }

    @permission(
        expression: true,
        actions: [list, update, create, get]
    ) 
}

